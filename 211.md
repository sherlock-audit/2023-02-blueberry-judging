rbserver

medium

# Calling `IchiVaultSpell.withdrawInternal` function can possibly swap tokens in an unexpected manner because `UniV3WrappedLibMockup` is used

## Summary
Calling the `IchiVaultSpell.withdrawInternal` function executes `isTokenA ? UniV3WrappedLibMockup.MAX_SQRT_RATIO - 1 : UniV3WrappedLibMockup.MIN_SQRT_RATIO + 1`, where `UniV3WrappedLibMockup` corresponds to `../libraries/UniV3/UniV3WrappedLibMockup.sol`. Because `../libraries/UniV3/UniV3WrappedLibMockup.sol` is a mockup file, it is possible that its `MAX_SQRT_RATIO` or `MIN_SQRT_RATIO` will be changed in the future for testing purpose. In this case, calling the `IchiVaultSpell.withdrawInternal` function would swap the tokens in an unexpected manner in production.

## Vulnerability Detail
When `MAX_SQRT_RATIO` or `MIN_SQRT_RATIO` in `../libraries/UniV3/UniV3WrappedLibMockup.sol` is changed for testing purpose, calling the `IchiVaultSpell.withdrawInternal` function, which executes `isTokenA ? UniV3WrappedLibMockup.MAX_SQRT_RATIO - 1 : UniV3WrappedLibMockup.MIN_SQRT_RATIO + 1`, can use an unexpected `sqrtPriceLimitX96` input value for the `swapPool.swap` function call in production.

## Impact
For the described scenario, calling the `IchiVaultSpell.withdrawInternal` function would swap the tokens in an unexpected manner, which can result in a partial swap that swaps insufficient token amount.

## Code Snippet
https://github.com/sherlock-audit/2023-02-blueberry/blob/main/contracts/spell/IchiVaultSpell.sol#L276-L330
```solidity
    function withdrawInternal(
        uint256 strategyId,
        address collToken,
        address borrowToken,
        uint256 amountRepay,
        uint256 amountLpWithdraw,
        uint256 amountShareWithdraw
    ) internal {
        Strategy memory strategy = strategies[strategyId];
        IICHIVault vault = IICHIVault(strategy.vault);
        uint256 positionId = bank.POSITION_ID();

        // 1. Compute repay amount if MAX_INT is supplied (max debt)
        if (amountRepay == type(uint256).max) {
            amountRepay = bank.borrowBalanceCurrent(positionId, borrowToken);
        }

        // 2. Calculate actual amount to remove
        uint256 amtLPToRemove = vault.balanceOf(address(this)) -
            amountLpWithdraw;

        // 3. Withdraw liquidity from ICHI vault
        vault.withdraw(amtLPToRemove, address(this));

        // 4. Swap withdrawn tokens to initial deposit token
        bool isTokenA = vault.token0() == borrowToken;
        uint256 amountToSwap = IERC20(
            isTokenA ? vault.token1() : vault.token0()
        ).balanceOf(address(this));
        if (amountToSwap > 0) {
            swapPool = IUniswapV3Pool(vault.pool());
            swapPool.swap(
                address(this),
                // if withdraw token is Token0, then swap token1 -> token0 (false)
                !isTokenA,
                int256(amountToSwap),
                isTokenA
                    ? UniV3WrappedLibMockup.MAX_SQRT_RATIO - 1 // Token0 -> Token1
                    : UniV3WrappedLibMockup.MIN_SQRT_RATIO + 1, // Token1 -> Token0
                abi.encode(address(this))
            );
        }

        // 5. Withdraw isolated collateral from Bank
        doWithdraw(collToken, amountShareWithdraw);

        // 6. Repay
        doRepay(borrowToken, amountRepay);

        _validateMaxLTV(strategyId);

        // 7. Refund
        doRefund(borrowToken);
        doRefund(collToken);
    }
```

https://github.com/sherlock-audit/2023-02-blueberry/blob/main/contracts/spell/IchiVaultSpell.sol#L13
```solidity
import "../libraries/UniV3/UniV3WrappedLibMockup.sol";
```

## Tool used
Manual Review

## Recommendation
Instead of `UniV3WrappedLibMockup`, the `UniV3WrappedLib` library should be imported in the `IchiVaultSpell` contract and used in the `IchiVaultSpell.withdrawInternal` function.