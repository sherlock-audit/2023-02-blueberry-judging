Bauer

high

# When user withdraw assets from ICHI Vault ,if the actual amount to remove in the value is less than lpTakeAmt ,then the user will lose the liquidity

## Summary
When user call ```closePosition()``` to withdraw assets from ICHI Vault,  the protocol first take out collateral and transfer the  corresponding amount of vault lptokens to ```IchiVaultSpell.sol```.Then remove the liquidity， withdraw isolated collateral from Bank，repay and refund. When remove the liquidity , the actual amount to remove is calculated by the formula ``` uint256 amtLPToRemove = vault.balanceOf(address(this)) - amountLpWithdraw```. If the amount of vault lptoken is bigger than the actual amount to remove ,it means the excess liquidity will be left. The next  user to call closePosition() can get this liquidity and profit。

## Vulnerability Detail
```solidity
function closePosition(
        uint256 strategyId,
        address collToken,
        address borrowToken,
        uint256 lpTakeAmt,
        uint256 amountRepay,
        uint256 amountLpWithdraw,
        uint256 amountShareWithdraw
    )
        external
        existingStrategy(strategyId)
        existingCollateral(strategyId, collToken)
    {
        // 1. Take out collateral
        doTakeCollateral(strategies[strategyId].vault, lpTakeAmt);

        withdrawInternal(
            strategyId,
            collToken,
            borrowToken,
            amountRepay,
            amountLpWithdraw,
            amountShareWithdraw
        );
    }
```
The protocol allows user to withdraw assets from ICHI Vault by calling ```closePosition()``` or ```closePositionFarm()```, it first take out collateral and transfer the  corresponding amount of  vault lptoken to ```IchiVaultSpell.sol``` .
```solidity
    function doTakeCollateral(address token, uint256 amount) internal {
        if (amount > 0) {
            amount = bank.takeCollateral(amount);
            werc20.burn(token, amount);
        }
    }

```

 Next ,call ```withdrawInternal()``` to process liquidity removal ， collateral withdrawals ，repay and refund business.
```solidity
 function withdrawInternal(
        uint256 strategyId,
        address collToken,
        address borrowToken,
        uint256 amountRepay,
        uint256 amountLpWithdraw,
        uint256 amountShareWithdraw
    ) internal {
        Strategy memory strategy = strategies[strategyId];
        IICHIVault vault = IICHIVault(strategy.vault);
        uint256 positionId = bank.POSITION_ID();

        // 1. Compute repay amount if MAX_INT is supplied (max debt)
        if (amountRepay == type(uint256).max) {
            amountRepay = bank.borrowBalanceCurrent(positionId, borrowToken);
        }

        // 2. Calculate actual amount to remove
        uint256 amtLPToRemove = vault.balanceOf(address(this)) -
            amountLpWithdraw;

        // 3. Withdraw liquidity from ICHI vault
        vault.withdraw(amtLPToRemove, address(this));

        // 4. Swap withdrawn tokens to initial deposit token
        bool isTokenA = vault.token0() == borrowToken;
        uint256 amountToSwap = IERC20(
            isTokenA ? vault.token1() : vault.token0()
        ).balanceOf(address(this));
        if (amountToSwap > 0) {
            swapPool = IUniswapV3Pool(vault.pool());
            swapPool.swap(
                address(this),
                // if withdraw token is Token0, then swap token1 -> token0 (false)
                !isTokenA,
                int256(amountToSwap),
                isTokenA
                    ? UniV3WrappedLibMockup.MAX_SQRT_RATIO - 1 // Token0 -> Token1
                    : UniV3WrappedLibMockup.MIN_SQRT_RATIO + 1, // Token1 -> Token0
                abi.encode(address(this))
            );
        }

        // 5. Withdraw isolated collateral from Bank
        doWithdraw(collToken, amountShareWithdraw);

        // 6. Repay
        doRepay(borrowToken, amountRepay);

        _validateMaxLTV(strategyId);

        // 7. Refund
        doRefund(borrowToken);
        doRefund(collToken);
    }
```
When remove the liquidity , the actual amount to remove is calculated by the below formula 
``` uint256 amtLPToRemove = vault.balanceOf(address(this)) - amountLpWithdraw```. 

If the amount of lptoken transfered to the ````IchiVaultSpell.sol``` is bigger than the actual amount to remove, after the protocol to complete  the liquidity removal ,collateral withdrawals ,repay and refund business.There will be excess liquidity saved in the protocol. Next user to call ```closePosition()``` can get the liquidity and swap it to token0 or token1 and profit.

## Impact
Users will lose liquidity

## Code Snippet
https://github.com/sherlock-audit/2023-02-blueberry/blob/main/contracts/spell/IchiVaultSpell.sol#L294-L295

## Tool used

Manual Review

## Recommendation
```amtLPToRemove =lpTakeAmt```
