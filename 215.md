rbserver

medium

# Calling `AggregatorOracle.getPrice` function reverts though it should not when Band protocol's reported price is valid and Chainlink's reported price is 0 and invalid

## Summary
When the Band protocol's reported price is valid and the Chainlink's reported price is 0 and invalid, calling the `AggregatorOracle.getPrice` function reverts though it should not.

## Vulnerability Detail
The `ChainlinkAdapterOracle.getPrice` function returns `(answer.toUint256() * 1e18) / 10**decimals`. When the Chainlink's reported `answer` is 0, which should be considered as invalid, `answer.toUint256()` is 0 based on OpenZeppelin's `SafeCast.toUint256` function shown in the Code Snippet section. When calling the `AggregatorOracle.getPrice` function, such 0 price returned by the `ChainlinkAdapterOracle.getPrice` function is still considered valid when it should not be. When this function further executes `if ((prices[1] * 1e18) / prices[0] > maxPriceDeviation) revert EXCEED_DEVIATION()` or `bool midMinOk = (prices[1] * 1e18) / prices[0]`, such execution reverts because of the division by `prices[0]` that is 0. This means when the Band protocol's reported price is valid and the Chainlink's reported price is 0 and invalid, calling the `AggregatorOracle.getPrice` function reverts, and the Band protocol's valid reported price cannot be used.

## Impact
When the Band protocol's reported price is valid and the Chainlink's reported price is 0 and invalid, calling the `AggregatorOracle.getPrice` function should not revert because there is one valid data source. However, as described in the Vulnerability Detail section, this is not the case. As a result, critical user actions, such as lending, withdrawing, borrowing, repaying, and liquidating, that should be allowed when at least one valid reported price exists become disallowed unexpectedly.

## Code Snippet
https://github.com/sherlock-audit/2023-02-blueberry/blob/main/contracts/oracle/ChainlinkAdapterOracle.sol#L66-L84
```solidity
    function getPrice(address _token) external view override returns (uint256) {
        // remap token if possible
        address token = remappedTokens[_token];
        if (token == address(0)) token = _token;

        uint256 maxDelayTime = maxDelayTimes[token];
        if (maxDelayTime == 0) revert NO_MAX_DELAY(_token);

        // try to get token-USD price
        uint256 decimals = registry.decimals(token, USD);
        (, int256 answer, , uint256 updatedAt, ) = registry.latestRoundData(
            token,
            USD
        );
        if (updatedAt < block.timestamp - maxDelayTime)
            revert PRICE_OUTDATED(_token);

        return (answer.toUint256() * 1e18) / 10**decimals;
    }
```

https://github.com/OpenZeppelin/openzeppelin-contracts/blob/master/contracts/utils/math/SafeCast.sol#L559-L562
```solidity
    function toUint256(int256 value) internal pure returns (uint256) {
        require(value >= 0, "SafeCast: value must be positive");
        return uint256(value);
    }
```

https://github.com/sherlock-audit/2023-02-blueberry/blob/main/contracts/oracle/AggregatorOracle.sol#L89-L142
```solidity
    function getPrice(address token) external view override returns (uint256) {
        uint256 candidateSourceCount = primarySourceCount[token];
        if (candidateSourceCount == 0) revert NO_PRIMARY_SOURCE(token);
        uint256[] memory prices = new uint256[](candidateSourceCount);

        // Get valid oracle sources
        uint256 validSourceCount = 0;
        for (uint256 idx = 0; idx < candidateSourceCount; idx++) {
            try primarySources[token][idx].getPrice(token) returns (
                uint256 px
            ) {
                prices[validSourceCount++] = px;
            } catch {}
        }
        if (validSourceCount == 0) revert NO_VALID_SOURCE(token);
        for (uint256 i = 0; i < validSourceCount - 1; i++) {
            for (uint256 j = 0; j < validSourceCount - i - 1; j++) {
                if (prices[j] > prices[j + 1]) {
                    (prices[j], prices[j + 1]) = (prices[j + 1], prices[j]);
                }
            }
        }
        uint256 maxPriceDeviation = maxPriceDeviations[token];

        // Algo:
        // - 1 valid source --> return price
        // - 2 valid sources
        //     --> if the prices within deviation threshold, return average
        //     --> else revert
        // - 3 valid sources --> check deviation threshold of each pair
        //     --> if all within threshold, return median
        //     --> if one pair within threshold, return average of the pair
        //     --> if none, revert
        // - revert otherwise
        if (validSourceCount == 1) {
            return prices[0]; // if 1 valid source, return
        } else if (validSourceCount == 2) {
            if ((prices[1] * 1e18) / prices[0] > maxPriceDeviation)
                revert EXCEED_DEVIATION();
            return (prices[0] + prices[1]) / 2; // if 2 valid sources, return average
        } else {
            bool midMinOk = (prices[1] * 1e18) / prices[0] <= maxPriceDeviation;
            bool maxMidOk = (prices[2] * 1e18) / prices[1] <= maxPriceDeviation;
            if (midMinOk && maxMidOk) {
                return prices[1]; // if 3 valid sources, and each pair is within thresh, return median
            } else if (midMinOk) {
                return (prices[0] + prices[1]) / 2; // return average of pair within thresh
            } else if (maxMidOk) {
                return (prices[1] + prices[2]) / 2; // return average of pair within thresh
            } else {
                revert EXCEED_DEVIATION();
            }
        }
    }
```

## Tool used
Manual Review

## Recommendation
The `ChainlinkAdapterOracle.getPrice` function can be updated to revert when `answer` returned by the `registry.latestRoundData` function call is 0.