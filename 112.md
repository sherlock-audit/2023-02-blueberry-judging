koxuan

medium

# stuck underlying tokens in BlueBerryBank when withdrawLend is called

## Summary
When a user closes a position that triggers a `withdrawLend` which withdraws their isolated collateral, vault withdraws the share which in turn withdraws from compound forked protocol. In the event that there is more underlying token withdrawn than when depositing, the user will only get back the deposited amount and the excess will stay in BlueBerryBank. The excess amount will not be utilized by BlueBerryBank and there is no way to withdraw it, causing loss of fund.

## Vulnerability Detail
When user withdraw isolated collateral, it will end up with `withdrawLend` being called in `BlueBerryBank`.
```solidity
    function withdrawLend(address token, uint256 shareAmount)
        external
        override
        inExec
        poke(token)
    {
        Position storage pos = positions[POSITION_ID];
        Bank storage bank = banks[token];
        if (token != pos.underlyingToken) revert INVALID_UTOKEN(token);
        if (shareAmount == type(uint256).max) {
            shareAmount = pos.underlyingVaultShare;
        }

        uint256 wAmount;
        if (address(ISoftVault(bank.softVault).uToken()) == token) {
            ISoftVault(bank.softVault).approve(
                bank.softVault,
                type(uint256).max
            );
            wAmount = ISoftVault(bank.softVault).withdraw(shareAmount);
        } else {
            wAmount = IHardVault(bank.hardVault).withdraw(token, shareAmount);
        }

        wAmount = wAmount > pos.underlyingAmount
            ? pos.underlyingAmount
            : wAmount;

        pos.underlyingVaultShare -= shareAmount;
        pos.underlyingAmount -= wAmount;
        bank.totalLend -= wAmount;

        wAmount = doCutWithdrawFee(token, wAmount);

        IERC20Upgradeable(token).safeTransfer(msg.sender, wAmount);
    }

```

Notice how softVault is being used to withdraw the shares that user lent to the compound forked protocol. The underlying token will be returned and notice how it is being compared to user original lent amount. If the returned amount is more in the event of share prices increasing in compound forked protocol, the user will only receive their original lent amount. The excess stays in the `BlueBerryBank`. The excess cannot be withdrawn and will not be utilized by `BlueBerryBank`, causing a loss of assets to protocol.

```solidity
    function withdraw(uint256 shareAmount)
        external
        override
        nonReentrant
        returns (uint256 withdrawAmount)
    {
        if (shareAmount == 0) revert ZERO_AMOUNT();

        _burn(msg.sender, shareAmount);

        uint256 uBalanceBefore = uToken.balanceOf(address(this));
        if (cToken.redeem(shareAmount) != 0) revert REDEEM_FAILED(shareAmount);
        uint256 uBalanceAfter = uToken.balanceOf(address(this));

        withdrawAmount = uBalanceAfter - uBalanceBefore;
        // Cut withdraw fee if it is in withdrawVaultFee Window (2 months)
        if (
            block.timestamp <
            config.withdrawVaultFeeWindowStartTime() +
                config.withdrawVaultFeeWindow()
        ) {
            uint256 fee = (withdrawAmount * config.withdrawVaultFee()) /
                DENOMINATOR;
            uToken.safeTransfer(config.treasury(), fee);
            withdrawAmount -= fee;
        }
        uToken.safeTransfer(msg.sender, withdrawAmount);

        emit Withdrawn(msg.sender, withdrawAmount, shareAmount);
    }

```

Hard Vault is omitted because I believe the implementation that connects hard vault to compound forked protocol is omitted. In the event that the compound forked protocol is implemented to allow deposited assets to hard vault to be lent to lending protocol, this issue will also affect hard vault underlying tokens. 




## Impact
Loss of assets to protocol when withdrawing lend for user.

## Code Snippet
[BlueBerryBank.sol#L669-L704](https://github.com/sherlock-audit/2023-02-blueberry/blob/main/contracts/BlueBerryBank.sol#L669-L704)
[SoftVault.sol#L94-L124](https://github.com/sherlock-audit/2023-02-blueberry/blob/main/contracts/vault/SoftVault.sol#L94-L124)
## Tool used

Manual Review

## Recommendation

If there are excess tokens, recommend sending it to treasury or utilizing it to prevent loss of excess tokens for protocol.