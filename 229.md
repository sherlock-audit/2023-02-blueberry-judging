koxuan

high

# incorrect logic in withdrawInternal can cause wrong amount lp to be withdrawn from IchiVault for swap

## Summary
According to the meaning of the variable and comment, `amountLpWithdraw` means Amount of ICHI Vault LP to withdraw from ICHI Vault. However, an incorrect implementation in `withdrawInternal` causes the incorrect amount to be withdrawn instead of `amountLpWithdraw` from IchiVault.

## Vulnerability Detail

As we see for `closePosition` comments, `amountLpWithdraw` refers to the Amount of ICHI Vault LP to withdraw from ICHI Vault.

```solidity
    /**
     * @notice External function to withdraw assets from ICHI Vault
     * @param collToken Token address to withdraw (e.g USDC)
     * @param borrowToken Token address to withdraw (e.g USDC)
     * @param lpTakeAmt Amount of ICHI Vault LP token to take out from Bank
     * @param amountRepay Amount to repay the loan
     * @param amountLpWithdraw Amount of ICHI Vault LP to withdraw from ICHI Vault
     * @param amountShareWithdraw Amount of Isolated collateral to withdraw from Compound
     */
    function closePosition(
        uint256 strategyId,
        address collToken,
        address borrowToken,
        uint256 lpTakeAmt,
        uint256 amountRepay,
        uint256 amountLpWithdraw,
        uint256 amountShareWithdraw
    )
        external
        existingStrategy(strategyId)
        existingCollateral(strategyId, collToken)
    {
        // 1. Take out collateral
        doTakeCollateral(strategies[strategyId].vault, lpTakeAmt);


        withdrawInternal(
            strategyId,
            collToken,
            borrowToken,
            amountRepay,
            amountLpWithdraw,
            amountShareWithdraw
        );
    }
```

However, when we look at `withdrawInternal`, instead of withdrawing the amount specified by user, the amount that is removed is based on the ichi vault liquidity balance that the position has minus the `amountLpWithdraw`. 

```solidity
        // 2. Calculate actual amount to remove
        uint256 amtLPToRemove = vault.balanceOf(address(this)) -
            amountLpWithdraw;


        // 3. Withdraw liquidity from ICHI vault
        vault.withdraw(amtLPToRemove, address(this));
```

```solidity
        // 4. Swap withdrawn tokens to initial deposit token
        bool isTokenA = vault.token0() == borrowToken;
        uint256 amountToSwap = IERC20(
            isTokenA ? vault.token1() : vault.token0()
        ).balanceOf(address(this));
        if (amountToSwap > 0) {
            swapPool = IUniswapV3Pool(vault.pool());
            swapPool.swap(
                address(this),
                // if withdraw token is Token0, then swap token1 -> token0 (false)
                !isTokenA,
                int256(amountToSwap),
                isTokenA
                    ? UniV3WrappedLibMockup.MAX_SQRT_RATIO - 1 // Token0 -> Token1
                    : UniV3WrappedLibMockup.MIN_SQRT_RATIO + 1, // Token1 -> Token0
                abi.encode(address(this))
            );
        }
```



## Impact
Wrong amount of withdrawn tokens is being swapped,  causing revert or worse the swap going through with the incorrect amount against the position owner intentions.

## Code Snippet
[IchiVaultSpell.sol#L332-L365](https://github.com/sherlock-audit/2023-02-blueberry/blob/main/contracts/spell/IchiVaultSpell.sol#L332-L365)
[IchiVaultSpell.sol#L293-L298](https://github.com/sherlock-audit/2023-02-blueberry/blob/main/contracts/spell/IchiVaultSpell.sol#L293-L298)
[IchiVaultSpell.sol#L300-L317](https://github.com/sherlock-audit/2023-02-blueberry/blob/main/contracts/spell/IchiVaultSpell.sol#L300-L317)
## Tool used

Manual Review

## Recommendation
Recommend using `amountLpWithdraw` as the amount to be withdrawn and swapped. 
