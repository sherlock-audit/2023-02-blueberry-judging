rbserver

medium

# Average between Band protocol's and Chainlink's reported prices can be unreliable

## Summary
The centralized exchanges can be a major data source for the Band protocol's price feed. For example, https://data.bandprotocol.com/symbol/USDC mentions that "Band Standard Dataset aggregates cryptoasset spot prices from 3 of the most reputable price aggregator sites (Brave New Coin, CoinGecko, CryptoCompare, and CoinMarketCap) along with 9 major exchanges (Binance, Huobi Pro, Coinbase Pro, Kraken, Bitfinex, Bittrex, Bitstamp, OKEX, and FTX)"; the 9 exchanges are the majority of the Band protocol's data sources. Because it is possible that the centralized exchanges would manipulate spot prices, the Band protocol's price feeds can be less reliable. Yet, for a token, the Band protocol's reported price is allowed to deviate from the Chainlink's reported price by up to 20% when calculating the average between the Band protocol's and Chainlink's reported prices. If the Band protocol's reported price is manipulated, such average price, which is affected by the Band protocol's manipulated reported price, can be unreliable because even a price deviation of 5%, which is used in the deployment script shown in the Code Snippet section, can be quite large already.

## Vulnerability Detail
As shown in the Code Snippet section, the `AggregatorOracle.getPrice` function would return the average between the Band protocol's and Chainlink's reported prices for a token when the price deviation among them does not exceed `maxPriceDeviation`. This `maxPriceDeviation` is configured and is allowed to be just less than `MAX_PRICE_DEVIATION`, which represents a 20% price deviation. For a token, when the centralized exchanges manipulate the spot price that causes the Band protocol's reported price to be manipulated while such price does not deviate from the Chainlink's reported price by more than `maxPriceDeviation`, the average between the Band protocol's and Chainlink's reported prices would be returned by the `AggregatorOracle.getPrice` function. However, because a price deviation of 5% that could be used for deployment can be quite large already, such average price, which is influenced by the Band protocol's manipulated reported price, is not reliable.

## Impact
When the average price that is affected by the Band protocol's manipulated reported price is used, critical user actions, such as lending, withdrawing, borrowing, repaying, and liquidating, that should be disallowed if only using the Chainlink's reliable reported price can be allowed, or vice versa.

## Code Snippet
https://github.com/sherlock-audit/2023-02-blueberry/blob/main/contracts/oracle/AggregatorOracle.sol#L89-L142
```solidity
    function getPrice(address token) external view override returns (uint256) {
        uint256 candidateSourceCount = primarySourceCount[token];
        if (candidateSourceCount == 0) revert NO_PRIMARY_SOURCE(token);
        uint256[] memory prices = new uint256[](candidateSourceCount);

        // Get valid oracle sources
        uint256 validSourceCount = 0;
        for (uint256 idx = 0; idx < candidateSourceCount; idx++) {
            try primarySources[token][idx].getPrice(token) returns (
                uint256 px
            ) {
                prices[validSourceCount++] = px;
            } catch {}
        }
        if (validSourceCount == 0) revert NO_VALID_SOURCE(token);
        for (uint256 i = 0; i < validSourceCount - 1; i++) {
            for (uint256 j = 0; j < validSourceCount - i - 1; j++) {
                if (prices[j] > prices[j + 1]) {
                    (prices[j], prices[j + 1]) = (prices[j + 1], prices[j]);
                }
            }
        }
        uint256 maxPriceDeviation = maxPriceDeviations[token];

        // Algo:
        // - 1 valid source --> return price
        // - 2 valid sources
        //     --> if the prices within deviation threshold, return average
        //     --> else revert
        // - 3 valid sources --> check deviation threshold of each pair
        //     --> if all within threshold, return median
        //     --> if one pair within threshold, return average of the pair
        //     --> if none, revert
        // - revert otherwise
        if (validSourceCount == 1) {
            return prices[0]; // if 1 valid source, return
        } else if (validSourceCount == 2) {
            if ((prices[1] * 1e18) / prices[0] > maxPriceDeviation)
                revert EXCEED_DEVIATION();
            return (prices[0] + prices[1]) / 2; // if 2 valid sources, return average
        } else {
            bool midMinOk = (prices[1] * 1e18) / prices[0] <= maxPriceDeviation;
            bool maxMidOk = (prices[2] * 1e18) / prices[1] <= maxPriceDeviation;
            if (midMinOk && maxMidOk) {
                return prices[1]; // if 3 valid sources, and each pair is within thresh, return median
            } else if (midMinOk) {
                return (prices[0] + prices[1]) / 2; // return average of pair within thresh
            } else if (maxMidOk) {
                return (prices[1] + prices[2]) / 2; // return average of pair within thresh
            } else {
                revert EXCEED_DEVIATION();
            }
        }
    }
```

https://github.com/sherlock-audit/2023-02-blueberry/blob/main/contracts/oracle/AggregatorOracle.sol#L24-L25
```solidity
    uint256 public constant MIN_PRICE_DEVIATION = 1e18; // min price deviation
    uint256 public constant MAX_PRICE_DEVIATION = 1.2e18; // max price deviation, 20%
```

https://github.com/sherlock-audit/2023-02-blueberry/blob/main/deploy/mainnet/0-deploy-oracle.ts#L96-L118
```typescript
  await aggregatorOracle.setMultiPrimarySources(
    [
      ADDRESS.USDC,
      ADDRESS.DAI,
      ADDRESS.CRV,
      ADDRESS.SUSHI,
      ADDRESS.WBTC,
      ADDRESS.WETH,
    ], [
    ethers.utils.parseEther("1.05"),
    ethers.utils.parseEther("1.05"),
    ethers.utils.parseEther("1.05"),
    ethers.utils.parseEther("1.05"),
    ethers.utils.parseEther("1.05"),
    ethers.utils.parseEther("1.05"),
  ], [
    [bandOracle.address, chainlinkOracle.address],
    [bandOracle.address, chainlinkOracle.address],
    [bandOracle.address, chainlinkOracle.address],
    [bandOracle.address, chainlinkOracle.address],
    [bandOracle.address, chainlinkOracle.address],
    [bandOracle.address, chainlinkOracle.address],
  ]);
```

## Tool used
Manual Review

## Recommendation
Instead of using the Band protocol's reported price for computing the average price, the Band protocol's price feed can be used as a fallback oracle for the Chainlink's price feed. When getting the Chainlink's reported price succeeds, the Band protocol's price feed would not be used; when getting the Chainlink's reported price fails, the Band protocol's reported price can then be used.