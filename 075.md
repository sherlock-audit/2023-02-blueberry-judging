rvierdiiev

high

# User lose WERC20 when close position and provides amountLpWithdraw that is not 0

## Summary
User lose WERC20 when close position and provides amountLpWithdraw that is not 0
## Vulnerability Detail
Let's check `IchiVaultSpell.closePosition` function. It first [withdraws `lpTakeAmt` amount of werc20 token](https://github.com/sherlock-audit/2023-02-blueberry/blob/main/contracts/spell/IchiVaultSpell.sol#L355). This actually just transfer werc20 from bank to IchiVaultSpell and then burns it in order to receive ichi vault tokens.
Then it calls `withdrawInternal` and pass params provided by user.

https://github.com/sherlock-audit/2023-02-blueberry/blob/main/contracts/spell/IchiVaultSpell.sol#L276-L330
```solidity
    function withdrawInternal(
        uint256 strategyId,
        address collToken,
        address borrowToken,
        uint256 amountRepay,
        uint256 amountLpWithdraw,
        uint256 amountShareWithdraw
    ) internal {
        Strategy memory strategy = strategies[strategyId];
        IICHIVault vault = IICHIVault(strategy.vault);
        uint256 positionId = bank.POSITION_ID();


        // 1. Compute repay amount if MAX_INT is supplied (max debt)
        if (amountRepay == type(uint256).max) {
            amountRepay = bank.borrowBalanceCurrent(positionId, borrowToken);
        }


        // 2. Calculate actual amount to remove
        uint256 amtLPToRemove = vault.balanceOf(address(this)) -
            amountLpWithdraw;


        // 3. Withdraw liquidity from ICHI vault
        vault.withdraw(amtLPToRemove, address(this));


        // 4. Swap withdrawn tokens to initial deposit token
        bool isTokenA = vault.token0() == borrowToken;
        uint256 amountToSwap = IERC20(
            isTokenA ? vault.token1() : vault.token0()
        ).balanceOf(address(this));
        if (amountToSwap > 0) {
            swapPool = IUniswapV3Pool(vault.pool());
            swapPool.swap(
                address(this),
                // if withdraw token is Token0, then swap token1 -> token0 (false)
                !isTokenA,
                int256(amountToSwap),
                isTokenA
                    ? UniV3WrappedLibMockup.MAX_SQRT_RATIO - 1 // Token0 -> Token1
                    : UniV3WrappedLibMockup.MIN_SQRT_RATIO + 1, // Token1 -> Token0
                abi.encode(address(this))
            );
        }


        // 5. Withdraw isolated collateral from Bank
        doWithdraw(collToken, amountShareWithdraw);


        // 6. Repay
        doRepay(borrowToken, amountRepay);


        _validateMaxLTV(strategyId);


        // 7. Refund
        doRefund(borrowToken);
        doRefund(collToken);
    }
```

What is interesting here is step 2 and 3. 
```solidity
        // 2. Calculate actual amount to remove
        uint256 amtLPToRemove = vault.balanceOf(address(this)) -
            amountLpWithdraw;


        // 3. Withdraw liquidity from ICHI vault
        vault.withdraw(amtLPToRemove, address(this));
```

As you can see `amountLpWithdraw` param, provided by user is used to calculate tokens that should be withdrawn from vault and then swapped with uniswap. In case if param is 0, then all balance will be withdrawn from vault and if not, then some part of user's tokens will be still there.

The problem is that nowhere in function in case if `amountLpWithdraw != 0`, vault.balanceOf(address(this)) is sent to user and also not put as collateral again.
So in this case, user just lose portion of vault tokens.

As i understand `amountLpWithdraw` is the amount that should be sent directly to user after all actions. In this case user loses this funds, so i guess it's high severity issue.
## Impact
User lose part of vault tokens
## Code Snippet
Provided above
## Tool used

Manual Review

## Recommendation
Send the rest to the user or put it back as collateral.