rbserver

medium

# Making a deposit to an ICHI vault that would cause the position size to exceed the vault's maximum position size can be incorrectly allowed

## Summary
Making a deposit to an ICHI vault that would cause the position size to exceed the vault's maximum position size should be disallowed. Yet, because `UniV3WrappedLibMockup` is used in the `UniswapV3AdapterOracle.getPrice` function, making such deposit can be incorrectly allowed.

## Vulnerability Detail
When calling the `IchiVaultSpell.depositInternal` function, `uint256 lpPrice = bank.oracle().getPrice(strategy.vault)` is executed so the `IchiLpOracle.getPrice` function is called. Calling the `IchiLpOracle.getPrice` function executes `uint256 px0 = base.getPrice(address(token0))`, where `token0` is ICHI, so the `UniswapV3AdapterOracle.getPrice` function is called.

However, when executing `(int24 arithmeticMeanTick, ) = UniV3WrappedLibMockup.consult(stablePool, secondsAgo)` in the `UniswapV3AdapterOracle.getPrice` function, the returned `arithmeticMeanTick` is 0 because the `UniV3WrappedLibMockup.consult` function is a mockup function that returns the default `arithmeticMeanTick` value, which is 0. Then, when executing `uint256 quoteTokenAmountForStable = UniV3WrappedLibMockup.getQuoteAtTick(arithmeticMeanTick, uint128(10**tokenDecimals), token, token1)` in the `UniswapV3AdapterOracle.getPrice` function, the returned `quoteTokenAmountForStable` is 0 because the `UniV3WrappedLibMockup.getQuoteAtTick` function, which is also a mockup function, returns the default `quoteAmount` value that is 0. When the `UniswapV3AdapterOracle.getPrice` function returns `(quoteTokenAmountForStable * base.getPrice(token1)) / 10**stableDecimals`, 0 is returned.

Because calling the `UniswapV3AdapterOracle.getPrice` function returns 0, `px0` is set to 0 in the `IchiLpOracle.getPrice` function. This means that, in the `IchiLpOracle.getPrice` function, `totalReserve` is set to a value that is lower than it should be, and `(totalReserve * 1e18) / totalSupply` returned by this function is also lower than it should be. Hence, `lpPrice` and `curPosSize` are set to values that are lower than they should be as well in the `IchiVaultSpell.depositInternal` function. When the `IchiVaultSpell.depositInternal` function executes `if (curPosSize > strategy.maxPositionSize) revert EXCEED_MAX_POS_SIZE(strategyId)`, since `curPosSize` is lower than it should be, the position size can be considered as not exceeding the maximum size of a position for the given ICHI vault after making the deposit though such maximum position size should already be exceeded.

## Impact
As described in the Vulnerability Detail section, the position size can be considered as not exceeding the given ICHI vault's maximum position size after making the deposit when such maximum position size should already be exceeded. As a result, making such deposit should be disallowed but is incorrectly allowed in this situation.

## Code Snippet
https://github.com/sherlock-audit/2023-02-blueberry/blob/main/contracts/spell/IchiVaultSpell.sol#L122-L157
```solidity
    function depositInternal(
        uint256 strategyId,
        address collToken,
        address borrowToken,
        uint256 collAmount,
        uint256 borrowAmount
    ) internal {
        Strategy memory strategy = strategies[strategyId];

        // 1. Lend isolated collaterals on compound
        doLend(collToken, collAmount);

        // 2. Borrow specific amounts
        doBorrow(borrowToken, borrowAmount);

        // 3. Add liquidity - Deposit on ICHI Vault
        IICHIVault vault = IICHIVault(strategy.vault);
        bool isTokenA = vault.token0() == borrowToken;
        uint256 balance = IERC20(borrowToken).balanceOf(address(this));
        ensureApprove(borrowToken, address(vault));
        if (isTokenA) {
            vault.deposit(balance, 0, address(this));
        } else {
            vault.deposit(0, balance, address(this));
        }

        // 4. Validate MAX LTV
        _validateMaxLTV(strategyId);

        // 5. Validate Max Pos Size
        uint256 lpPrice = bank.oracle().getPrice(strategy.vault);
        uint256 curPosSize = (lpPrice * vault.balanceOf(address(this))) /
            10**IICHIVault(strategy.vault).decimals();
        if (curPosSize > strategy.maxPositionSize)
            revert EXCEED_MAX_POS_SIZE(strategyId);
    }
```

https://github.com/sherlock-audit/2023-02-blueberry/blob/main/contracts/oracle/IchiLpOracle.sol#L19-L39
```solidity
    function getPrice(address token) external view override returns (uint256) {
        IICHIVault vault = IICHIVault(token);
        uint256 totalSupply = vault.totalSupply();
        if (totalSupply == 0) return 0;

        address token0 = vault.token0();
        address token1 = vault.token1();

        (uint256 r0, uint256 r1) = vault.getTotalAmounts();
        uint256 px0 = base.getPrice(address(token0));
        uint256 px1 = base.getPrice(address(token1));
        uint256 t0Decimal = IERC20Metadata(token0).decimals();
        uint256 t1Decimal = IERC20Metadata(token1).decimals();

        uint256 totalReserve = (r0 * px0) /
            10**t0Decimal +
            (r1 * px1) /
            10**t1Decimal;

        return (totalReserve * 1e18) / totalSupply;
    }
```

https://github.com/sherlock-audit/2023-02-blueberry/blob/main/contracts/oracle/UniswapV3AdapterOracle.sol#L41-L69
```solidity
    function getPrice(address token) external view override returns (uint256) {
        // Maximum cap of maxDelayTime is 2 days(172,800), safe to convert
        uint32 secondsAgo = uint32(maxDelayTimes[token]);
        if (secondsAgo == 0) revert NO_MEAN(token);

        address stablePool = stablePools[token];
        if (stablePool == address(0)) revert NO_STABLEPOOL(token);

        address token0 = IUniswapV3Pool(stablePool).token0();
        address token1 = IUniswapV3Pool(stablePool).token1();
        token1 = token0 == token ? token1 : token0; // get stable token address
        uint256 stableDecimals = uint256(IERC20Metadata(token1).decimals());
        uint256 tokenDecimals = uint256(IERC20Metadata(token).decimals());
        (int24 arithmeticMeanTick, ) = UniV3WrappedLibMockup.consult(
            stablePool,
            secondsAgo
        );
        uint256 quoteTokenAmountForStable = UniV3WrappedLibMockup
            .getQuoteAtTick(
                arithmeticMeanTick,
                uint128(10**tokenDecimals),
                token,
                token1
            );

        return
            (quoteTokenAmountForStable * base.getPrice(token1)) /
            10**stableDecimals;
    }
```

https://github.com/sherlock-audit/2023-02-blueberry/blob/main/contracts/oracle/UniswapV3AdapterOracle.sol#L13
```solidity
import "../libraries/UniV3/UniV3WrappedLibMockup.sol";
```

https://github.com/sherlock-audit/2023-02-blueberry/blob/main/contracts/libraries/UniV3/UniV3WrappedLibMockup.sol#L14-L18
```solidity
    function consult(address pool, uint32 secondsAgo)
        external
        view
        returns (int24 arithmeticMeanTick, uint128 harmonicMeanLiquidity)
    {}
```

https://github.com/sherlock-audit/2023-02-blueberry/blob/main/contracts/libraries/UniV3/UniV3WrappedLibMockup.sol#L20-L25
```solidity
    function getQuoteAtTick(
        int24 tick,
        uint128 baseAmount,
        address baseToken,
        address quoteToken
    ) external pure returns (uint256 quoteAmount) {}
```

## Tool used
Manual Review

## Recommendation
The `UniswapV3AdapterOracle` contract should import the `UniV3WrappedLib` library instead of `UniV3WrappedLibMockup` and call the `UniV3WrappedLib.consult` and `UniV3WrappedLib.getQuoteAtTick` functions in the `UniswapV3AdapterOracle.getPrice` function.